---
description: >-
  source from ã€ŠProfessional CUDA C Programmingã€‹By Max Grossman&Ty McKercher.I
  made some changes according to my own understanding.
---

# ğŸ¤­ Reduction|å¹¶è¡Œè§„çº¦

### å¹¶è¡Œå½’çº¦é—®é¢˜&#x20;

å‡è®¾è¦å¯¹ä¸€ä¸ªæœ‰Nä¸ªå…ƒç´ çš„æ•´æ•°æ•°ç»„æ±‚å’Œã€‚ä½¿ç”¨å¦‚ä¸‹çš„ä¸²è¡Œä»£ç å¾ˆå®¹æ˜“å®ç°ç®—æ³•ï¼š

```c
int sum = 0;
 for (int i = 0; i < N; i++)
   sum += array[i];
```

å¦‚æœæœ‰å¤§é‡çš„æ•°æ®å…ƒç´ ä¼šæ€ä¹ˆæ ·å‘¢ï¼Ÿå¦‚ä½•é€šè¿‡å¹¶è¡Œè®¡ç®—å¿«é€Ÿæ±‚å’Œå‘¢ï¼Ÿé‰´äºåŠ æ³•çš„ç»“åˆ å¾‹å’Œäº¤æ¢å¾‹ï¼Œæ•°ç»„å…ƒç´ å¯ä»¥ä»¥ä»»ä½•é¡ºåºæ±‚å’Œã€‚æ‰€ä»¥å¯ä»¥ç”¨ä»¥ä¸‹çš„æ–¹æ³•æ‰§è¡Œå¹¶è¡ŒåŠ æ³•è¿ç®—ï¼š&#x20;

1. å°†è¾“å…¥å‘é‡åˆ’åˆ†åˆ°æ›´å°çš„æ•°æ®å—ä¸­ã€‚&#x20;
2. ç”¨ä¸€ä¸ªçº¿ç¨‹è®¡ç®—ä¸€ä¸ªæ•°æ®å—çš„éƒ¨åˆ†å’Œã€‚
3. å¯¹æ¯ä¸ªæ•°æ®å—çš„éƒ¨åˆ†å’Œå†æ±‚å’Œå¾—å‡ºæœ€ç»ˆç»“æœã€‚&#x20;

å¹¶è¡ŒåŠ æ³•çš„ä¸€ä¸ªå¸¸ç”¨æ–¹æ³•æ˜¯ä½¿ç”¨è¿­ä»£æˆå¯¹å®ç°ã€‚ä¸€ä¸ªæ•°æ®å—åªåŒ…å«ä¸€å¯¹å…ƒç´ ï¼Œå¹¶ä¸”ä¸€ ä¸ªçº¿ç¨‹å¯¹è¿™ä¸¤ä¸ªå…ƒç´ æ±‚å’Œäº§ç”Ÿä¸€ä¸ªå±€éƒ¨ç»“æœã€‚ç„¶åï¼Œè¿™äº›å±€éƒ¨ç»“æœåœ¨æœ€åˆçš„è¾“å…¥å‘é‡ä¸­å°± åœ°ä¿å­˜ã€‚è¿™äº›æ–°å€¼è¢«ä½œä¸ºä¸‹ä¸€æ¬¡è¿­ä»£æ±‚å’Œçš„è¾“å…¥å€¼ã€‚<mark style="color:red;">å› ä¸ºè¾“å…¥å€¼çš„æ•°é‡åœ¨æ¯ä¸€æ¬¡è¿­ä»£åä¼š å‡åŠï¼Œå½“è¾“å‡ºå‘é‡çš„é•¿åº¦è¾¾åˆ°1æ—¶ï¼Œæœ€ç»ˆçš„å’Œå°±å·²ç»è¢«è®¡ç®—å‡ºæ¥äº†ã€‚</mark>

æ ¹æ®æ¯æ¬¡è¿­ä»£åè¾“å‡ºå…ƒç´ å°±åœ°å­˜å‚¨çš„ä½ç½®ï¼Œæˆå¯¹çš„å¹¶è¡Œæ±‚å’Œå®ç°å¯ä»¥è¢«è¿›ä¸€æ­¥åˆ†ä¸ºä»¥ ä¸‹ä¸¤ç§ç±»å‹ï¼š

* <mark style="color:red;">ç›¸é‚»é…å¯¹ï¼šå…ƒç´ ä¸å®ƒä»¬ç›´æ¥ç›¸é‚»çš„å…ƒç´ é…å¯¹</mark>
* <mark style="color:red;">äº¤é”™é…å¯¹ï¼šæ ¹æ®ç»™å®šçš„è·¨åº¦é…å¯¹å…ƒç´ </mark>

ä¸‹å›¾æ‰€ç¤ºä¸ºç›¸é‚»é…å¯¹çš„å®ç°ã€‚åœ¨æ¯ä¸€æ­¥å®ç°ä¸­ï¼Œä¸€ä¸ªçº¿ç¨‹å¯¹ä¸¤ä¸ªç›¸é‚»å…ƒç´ è¿›è¡Œæ“ ä½œï¼Œäº§ç”Ÿéƒ¨åˆ†å’Œã€‚å¯¹äºæœ‰Nä¸ªå…ƒç´ çš„æ•°ç»„ï¼Œè¿™ç§å®ç°æ–¹å¼éœ€è¦Nâ€•1æ¬¡æ±‚å’Œï¼Œè¿›è¡Œlog2N æ­¥ï¼Œæ—¶é—´å¤æ‚åº¦ä¸ºO(log2N)ã€‚

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (165).png" alt="" width="563"><figcaption></figcaption></figure>

ä¸‹å›¾æ‰€ç¤ºä¸ºäº¤é”™é…å¯¹çš„å®ç°ã€‚å€¼å¾—æ³¨æ„çš„æ˜¯ï¼Œåœ¨è¿™ç§å®ç°æ–¹æ³•çš„æ¯ä¸€æ­¥ä¸­ï¼Œä¸€ä¸ªçº¿ ç¨‹çš„è¾“å…¥æ˜¯è¾“å…¥æ•°ç»„é•¿åº¦çš„ä¸€åŠã€‚

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (166).png" alt="" width="563"><figcaption></figcaption></figure>

ä¸‹åˆ—çš„Cè¯­è¨€å‡½æ•°æ˜¯ä¸€ä¸ªäº¤é”™é…å¯¹æ–¹æ³•çš„é€’å½’å®ç°ï¼š

```c
 int recursiveReduce(int *data, int const size) {
   // terminate check
   if (size == 1) return data[0];
   // renew the stride
   int const stride = size / 2;
   // in-place reduction
   for (int i = 0; i < stride; i++) {
      data[i] += data[i + stride];
   }
   // call recursively
   return recursiveReduce(data, stride);
 }
```

<mark style="color:red;">å°½ç®¡ä»¥ä¸Šä»£ç å®ç°çš„æ˜¯åŠ æ³•ï¼Œä½†ä»»ä½•æ»¡è¶³äº¤æ¢å¾‹å’Œç»“åˆå¾‹çš„è¿ç®—éƒ½å¯ä»¥ä»£æ›¿åŠ æ³•ã€‚ä¾‹ å¦‚ï¼Œé€šè¿‡è°ƒç”¨maxä»£æ›¿æ±‚å’Œè¿ç®—ï¼Œå°±å¯ä»¥è®¡ç®—è¾“å…¥å‘é‡ä¸­çš„æœ€å¤§å€¼ã€‚å…¶ä»–æœ‰æ•ˆè¿ç®—çš„ä¾‹å­ æœ‰æœ€å°å€¼ã€å¹³å‡å€¼å’Œä¹˜ç§¯ã€‚</mark>&#x20;

åœ¨å‘é‡ä¸­æ‰§è¡Œæ»¡è¶³äº¤æ¢å¾‹å’Œç»“åˆå¾‹çš„è¿ç®—ï¼Œè¢«ç§°ä¸ºå½’çº¦é—®é¢˜ã€‚å¹¶è¡Œå½’çº¦é—®é¢˜æ˜¯è¿™ç§è¿ ç®—çš„å¹¶è¡Œæ‰§è¡Œã€‚å¹¶è¡Œå½’çº¦æ˜¯ä¸€ç§æœ€å¸¸è§çš„å¹¶è¡Œæ¨¡å¼ï¼Œå¹¶ä¸”æ˜¯è®¸å¤šå¹¶è¡Œç®—æ³•ä¸­çš„ä¸€ä¸ªå…³é”®è¿ ç®—ã€‚

### å¹¶è¡Œå½’çº¦ä¸­çš„åˆ†åŒ–

ä¸‹å›¾æ‰€ç¤ºçš„æ˜¯ç›¸é‚»é…å¯¹æ–¹æ³•çš„å†…æ ¸å®ç°æµç¨‹ã€‚æ¯ä¸ªçº¿ç¨‹å°†ç›¸é‚»çš„ä¸¤ä¸ªå…ƒç´ ç›¸åŠ äº§ç”Ÿ éƒ¨åˆ†å’Œã€‚ åœ¨è¿™ä¸ªå†…æ ¸é‡Œï¼Œæœ‰ä¸¤ä¸ªå…¨å±€å†…å­˜æ•°ç»„ï¼šä¸€ä¸ªå¤§æ•°ç»„ç”¨æ¥å­˜æ”¾æ•´ä¸ªæ•°ç»„ï¼Œè¿›è¡Œå½’çº¦ï¼›å¦ ä¸€ä¸ªå°æ•°ç»„ç”¨æ¥å­˜æ”¾æ¯ä¸ªçº¿ç¨‹å—çš„éƒ¨åˆ†å’Œã€‚

æ¯ä¸ªçº¿ç¨‹å—åœ¨æ•°ç»„çš„ä¸€éƒ¨åˆ†ä¸Šç‹¬ç«‹åœ°æ‰§è¡Œæ“ ä½œã€‚å¾ªç¯ä¸­è¿­ä»£ä¸€æ¬¡æ‰§è¡Œä¸€ä¸ªå½’çº¦æ­¥éª¤ã€‚å½’çº¦æ˜¯åœ¨å°±åœ°å®Œæˆçš„ï¼Œè¿™æ„å‘³ç€åœ¨æ¯ä¸€æ­¥ï¼Œå…¨å±€ å†…å­˜é‡Œçš„å€¼éƒ½è¢«éƒ¨åˆ†å’Œæ›¿ä»£ã€‚`__syncthreads`è¯­å¥å¯ä»¥ä¿è¯ï¼Œçº¿ç¨‹å—ä¸­çš„ä»»ä¸€çº¿ç¨‹åœ¨è¿›å…¥ä¸‹ ä¸€æ¬¡è¿­ä»£ä¹‹å‰ï¼Œåœ¨å½“å‰è¿­ä»£é‡Œæ¯ä¸ªçº¿ç¨‹çš„æ‰€æœ‰éƒ¨åˆ†å’Œéƒ½è¢«ä¿å­˜åœ¨äº†å…¨å±€å†…å­˜ä¸­ã€‚è¿›å…¥ä¸‹ä¸€ æ¬¡è¿­ä»£çš„æ‰€æœ‰çº¿ç¨‹éƒ½ä½¿ç”¨ä¸Šä¸€æ­¥äº§ç”Ÿçš„æ•°å€¼ã€‚åœ¨æœ€åä¸€ä¸ªå¾ªç¯ä»¥åï¼Œæ•´ä¸ªçº¿ç¨‹å—çš„å’Œè¢«ä¿ å­˜è¿›å…¨å±€å†…å­˜ä¸­ã€‚

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (167).png" alt=""><figcaption></figcaption></figure>

```c
 // Neighbored Pair Implementation with divergence
 // næ˜¯arrayæ•°ç»„Â·çš„æ€»é•¿åº¦
__global__ void reduceNeighbored (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x;

    // boundary check
    if (idx >= n) return;

    // in-place reduction in global memory
    // æ¯ä¸€ä¸ªblockè´Ÿè´£è®¡ç®—blockDim.xä¸ªæ•°æ®
    for (int stride = 1; stride < blockDim.x; stride *= 2)// æ§åˆ¶strideä¸å˜ï¼Œè®©tidå˜åŒ–
    {
        if ((tid % (2 * stride)) == 0)// tidä¸ºå¥‡æ•°çš„æƒ…å†µä¼šè¢«è¿‡æ»¤æ‰ï¼Œä¼šå‘ç”Ÿçº¿ç¨‹æŸåˆ†åŒ–
        {
            // idataä¸ºæ¯ä¸€ä¸ªblockçš„é¦–åœ°å€
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

```

{% hint style="info" %}
1. ç¬¬ä¸€æ¬¡çœ‹ä»£ç å¯èƒ½æœ‰äº›è¿·ç³Šï¼Œå¯ä»¥ç»“åˆæˆ‘çš„æ³¨é‡Šï¼Œè‡ªå·±åœ¨ç”»ç”»å›¾
2. å¯ä»¥ä»çº¿ç¨‹æŸæ˜¯å¦åˆ†åŒ–ï¼Œæ—¶é—´å±€éƒ¨æ€§å’Œç©ºé—´å±€éƒ¨æ€§æ€è€ƒä¸‹è¿™æ®µä»£ç å¯ä»¥æ€ä¹ˆæ”¹è¿›
{% endhint %}

ä¸¤ä¸ªç›¸é‚»å…ƒç´ é—´çš„è·ç¦»è¢«ç§°ä¸ºè·¨åº¦ï¼Œåˆå§‹åŒ–å‡ä¸º1ã€‚åœ¨æ¯ä¸€æ¬¡å½’çº¦å¾ªç¯ç»“æŸåï¼Œè¿™ä¸ª é—´éš”å°±è¢«ä¹˜ä»¥2ã€‚åœ¨ç¬¬ä¸€æ¬¡å¾ªç¯ç»“æŸåï¼Œidataï¼ˆå…¨å±€æ•°æ®æŒ‡é’ˆï¼‰çš„å¶æ•°å…ƒç´ å°†ä¼šè¢«éƒ¨åˆ†å’Œ æ›¿ä»£ã€‚åœ¨ç¬¬äºŒæ¬¡å¾ªç¯ç»“æŸåï¼Œidataçš„æ¯å››ä¸ªå…ƒç´ å°†ä¼šè¢«æ–°äº§ç”Ÿçš„éƒ¨åˆ†å’Œæ›¿ä»£ã€‚å› ä¸ºçº¿ç¨‹å— é—´æ— æ³•åŒæ­¥ï¼Œæ‰€ä»¥æ¯ä¸ªçº¿ç¨‹å—äº§ç”Ÿçš„éƒ¨åˆ†å’Œè¢«å¤åˆ¶å›äº†ä¸»æœºï¼Œå¹¶ä¸”åœ¨é‚£å„¿è¿›è¡Œä¸²è¡Œæ±‚å’Œï¼Œ å¦‚ä¸‹å›¾æ‰€ç¤ºã€‚

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (168).png" alt=""><figcaption></figcaption></figure>

### æ”¹å–„å¹¶è¡Œå½’çº¦çš„åˆ†åŒ–&#x20;

æµ‹è¯•æ ¸å‡½æ•°reduceNeighboredï¼Œå¹¶æ³¨æ„ä»¥ä¸‹æ¡ä»¶è¡¨è¾¾å¼ï¼š

```c
 if ((tid % (2 * stride)) == 0)
```

å› ä¸ºä¸Šè¿°è¯­å¥åªå¯¹å¶æ•°IDçš„çº¿ç¨‹ä¸ºtrueï¼Œæ‰€ä»¥è¿™ä¼šå¯¼è‡´å¾ˆé«˜çš„çº¿ç¨‹æŸåˆ†åŒ–ã€‚åœ¨å¹¶è¡Œå½’ çº¦çš„ç¬¬ä¸€æ¬¡è¿­ä»£ä¸­ï¼Œåªæœ‰IDä¸ºå¶æ•°çš„çº¿ç¨‹æ‰§è¡Œè¿™ä¸ªæ¡ä»¶è¯­å¥çš„ä¸»ä½“ï¼Œä½†æ˜¯æ‰€æœ‰çš„çº¿ç¨‹éƒ½å¿… é¡»è¢«è°ƒåº¦ã€‚åœ¨ç¬¬äºŒæ¬¡è¿­ä»£ä¸­ï¼Œåªæœ‰å››åˆ†ä¹‹ä¸€çš„çº¿ç¨‹æ˜¯æ´»è·ƒçš„ï¼Œä½†æ˜¯æ‰€æœ‰çš„çº¿ç¨‹ä»ç„¶éƒ½å¿…é¡» è¢«è°ƒåº¦ã€‚é€šè¿‡é‡æ–°ç»„ç»‡æ¯ä¸ªçº¿ç¨‹çš„æ•°ç»„ç´¢å¼•æ¥å¼ºåˆ¶IDç›¸é‚»çš„çº¿ç¨‹æ‰§è¡Œæ±‚å’Œæ“ä½œï¼Œçº¿ç¨‹æŸåˆ† åŒ–å°±èƒ½è¢«å½’çº¦äº†ã€‚ä¸‹å›¾å±•ç¤ºäº†è¿™ç§å®ç°ã€‚

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (169).png" alt=""><figcaption></figcaption></figure>

ä¿®æ”¹ä¹‹åçš„å†…æ ¸ä»£ç å¦‚ä¸‹ï¼š

```c
 __global__ void reduceNeighboredLess (int *g_idata, int *g_odata, unsigned int n) {
   // set thread ID
   unsigned int tid = threadIdx.x;
   unsigned int idx = blockIdx.x * blockDim.x + threadIdx.x;
   // convert global data pointer to the local pointer of this block
   int *idata = g_idata + blockIdx.x*blockDim.x;
   // boundary check
   if(idx >= n) return;
   // in-place reduction in global memory
   for (int stride = 1; stride < blockDim.x; stride *= 2) {
        // convert tid into local array index
      int index = 2 * stride * tid;
      if (index < blockDim.x) {
         idata[index] += idata[index + stride];
      }
      // synchronize within threadblock
      __syncthreads();
   }
   // write result for this block to global mem
   if (tid == 0) g_odata[blockIdx.x] = idata[0];
 }
```

æ³¨æ„å†…æ ¸ä¸­çš„ä¸‹è¿°è¯­å¥ï¼Œå®ƒä¸ºæ¯ä¸ªçº¿ç¨‹è®¾ç½®æ•°ç»„è®¿é—®ç´¢å¼•ï¼š

```c
int index = 2 * stride * tid;
```

å› ä¸ºè·¨åº¦ä¹˜ä»¥äº†2ï¼Œæ‰€ä»¥ä¸‹é¢çš„è¯­å¥ä½¿ç”¨çº¿ç¨‹å—çš„å‰åŠéƒ¨åˆ†æ¥æ‰§è¡Œæ±‚å’Œæ“ä½œï¼š

```c
if (index < blockDim.x)
```

> åœ¨å½’çº¦æ“ä½œä¸­ï¼Œä½¿ç”¨é‚»è¿‘å½’çº¦æ–¹æ³•æ—¶ï¼Œæ¯ä¸ªçº¿ç¨‹è´Ÿè´£å¤„ç†ç›¸é‚»çš„ä¸¤ä¸ªå…ƒç´ ã€‚é€šè¿‡ä¹˜ä»¥ 2ï¼Œå¯ä»¥ä½¿æ¯ä¸ªçº¿ç¨‹åœ¨æ•°ç»„ä¸­è·³è¿‡ä¸€ä¸ªå…ƒç´ ï¼Œä»è€Œå¤„ç†ç›¸é‚»çš„ä¸¤ä¸ªå…ƒç´ ã€‚è€ƒè™‘ä¸€ä¸ªç®€å•çš„ç¤ºä¾‹ï¼Œå‡è®¾æœ‰ä¸€ä¸ªåŒ…å« 8 ä¸ªå…ƒç´ çš„æ•°ç»„ï¼Œç¼–å·ä¸º 0 åˆ° 7ã€‚ä½¿ç”¨é‚»è¿‘å½’çº¦æ–¹æ³•æ—¶ï¼Œç¬¬ä¸€æ¬¡è¿­ä»£ä¸­ï¼Œçº¿ç¨‹ 0 è´Ÿè´£å¤„ç†å…ƒç´  0 å’Œå…ƒç´  1ï¼Œçº¿ç¨‹ 1 è´Ÿè´£å¤„ç†å…ƒç´  2 å’Œå…ƒç´  3ï¼Œä»¥æ­¤ç±»æ¨ã€‚é€šè¿‡å°† tid ä¹˜ä»¥ 2ï¼Œå¯ä»¥ä½¿æ¯ä¸ªçº¿ç¨‹çš„ç´¢å¼•è·³è¿‡ä¸€ä¸ªå…ƒç´ ã€‚å¯¹äºçº¿ç¨‹ 0ï¼Œtid ä¸º 0ï¼Œstride ä¸º 1ï¼Œå› æ­¤ index = 2 \* stride \* tid = 0ï¼Œçº¿ç¨‹ 0 å¤„ç†å…ƒç´  0 å’Œå…ƒç´  1ã€‚å¯¹äºçº¿ç¨‹ 1ï¼Œtid ä¸º 1ï¼Œstride ä¸º 1ï¼Œå› æ­¤ index = 2 \* stride \* tid = 2ï¼Œçº¿ç¨‹ 1 å¤„ç†å…ƒç´  2 å’Œå…ƒç´  3ã€‚åŒæ ·çš„æ–¹å¼ç»§ç»­ä¸‹å»ï¼Œæ¯ä¸ªçº¿ç¨‹å¤„ç†ç›¸é‚»çš„ä¸¤ä¸ªå…ƒç´ ï¼Œç›´åˆ°å®Œæˆå½’çº¦æ“ä½œã€‚ä¹˜ä»¥ 2 æ˜¯ä¸ºäº†ç¡®ä¿æ¯ä¸ªçº¿ç¨‹å¤„ç†ç›¸é‚»çš„å…ƒç´ ï¼Œè¿™æ˜¯é‚»è¿‘å½’çº¦æ–¹æ³•çš„è¦æ±‚ã€‚

å¯¹äºä¸€ä¸ªæœ‰512ä¸ªçº¿ç¨‹çš„å—æ¥è¯´ï¼Œå‰8ä¸ªçº¿ç¨‹æŸæ‰§è¡Œç¬¬ä¸€è½®å½’çº¦ï¼Œå‰©ä¸‹8ä¸ªçº¿ç¨‹æŸä»€ä¹ˆ ä¹Ÿä¸åšã€‚åœ¨ç¬¬äºŒè½®é‡Œï¼Œå‰4ä¸ªçº¿ç¨‹æŸæ‰§è¡Œå½’çº¦ï¼Œå‰©ä¸‹12ä¸ªçº¿ç¨‹æŸä»€ä¹ˆä¹Ÿä¸åšã€‚å› æ­¤ï¼Œè¿™æ · å°±å½»åº•ä¸å­˜åœ¨åˆ†åŒ–äº†ã€‚åœ¨æœ€åäº”è½®ä¸­ï¼Œå½“æ¯ä¸€è½®çš„çº¿ç¨‹æ€»æ•°å°äºçº¿ç¨‹æŸçš„å¤§å°æ—¶ï¼Œåˆ†åŒ–å°± ä¼šå‡ºç°ã€‚

### äº¤é”™é…å¯¹çš„å½’çº¦&#x20;

ä¸ç›¸é‚»é…å¯¹æ–¹æ³•ç›¸æ¯”ï¼Œäº¤é”™é…å¯¹æ–¹æ³•é¢ å€’äº†å…ƒç´ çš„è·¨åº¦ã€‚åˆå§‹è·¨åº¦æ˜¯çº¿ç¨‹å—å¤§å°çš„ä¸€ åŠï¼Œç„¶ååœ¨æ¯æ¬¡è¿­ä»£ä¸­å‡å°‘ä¸€åŠã€‚åœ¨æ¯æ¬¡å¾ªç¯ä¸­ï¼Œæ¯ä¸ªçº¿ç¨‹å¯¹ä¸¤ä¸ªè¢«å½“ å‰è·¨åº¦éš”å¼€çš„å…ƒç´ è¿›è¡Œæ±‚å’Œï¼Œä»¥äº§ç”Ÿä¸€ä¸ªéƒ¨åˆ†å’Œã€‚ä¸ç›¸é‚»è§„çº¦ï¼Œäº¤é”™å½’çº¦çš„å·¥ä½œçº¿ç¨‹æ²¡ æœ‰å˜åŒ–ã€‚ä½†æ˜¯ï¼Œæ¯ä¸ªçº¿ç¨‹åœ¨å…¨å±€å†…å­˜ä¸­çš„åŠ è½½/å­˜å‚¨ä½ç½®æ˜¯ä¸åŒçš„ã€‚

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (170).png" alt="" width="563"><figcaption></figcaption></figure>

äº¤é”™å½’çº¦çš„å†…æ ¸ä»£ç å¦‚ä¸‹æ‰€ç¤ºï¼š

```c
// Interleaved Pair Implementation with less divergence
__global__ void reduceInterleaved (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x;

    // boundary check
    if(idx >= n) return;

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 0; stride >>= 1)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}
```

æ³¨æ„æ ¸å‡½æ•°ä¸­çš„ä¸‹è¿°è¯­å¥ï¼Œä¸¤ä¸ªå…ƒç´ é—´çš„è·¨åº¦è¢«åˆå§‹åŒ–ä¸ºçº¿ç¨‹å—å¤§å°çš„ä¸€åŠï¼Œç„¶ååœ¨ æ¯æ¬¡å¾ªç¯ä¸­å‡å°‘ä¸€åŠï¼š

```c
for (int stride = blockDim.x / 2; stride > 0; sstride >>= 1)
```

ä¸‹é¢çš„è¯­å¥åœ¨ç¬¬ä¸€æ¬¡è¿­ä»£æ—¶å¼ºåˆ¶çº¿ç¨‹å—ä¸­çš„å‰åŠéƒ¨åˆ†çº¿ç¨‹æ‰§è¡Œæ±‚å’Œæ“ä½œï¼Œç¬¬äºŒæ¬¡è¿­ä»£ æ—¶æ˜¯çº¿ç¨‹å—çš„å‰å››åˆ†ä¹‹ä¸€ï¼Œä»¥æ­¤ç±»æ¨ï¼š

```c
if(tid < stride)
```

### å±•å¼€çš„è§„çº¦&#x20;

<mark style="color:red;">å¾ªç¯å±•å¼€æ˜¯ä¸€ä¸ªå°è¯•é€šè¿‡å‡å°‘åˆ†æ”¯å‡ºç°çš„é¢‘ç‡å’Œå¾ªç¯ç»´æŠ¤æŒ‡ä»¤æ¥ä¼˜åŒ–å¾ªç¯çš„æŠ€æœ¯ã€‚</mark>åœ¨ å¾ªç¯å±•å¼€ä¸­ï¼Œå¾ªç¯ä¸»ä½“åœ¨ä»£ç ä¸­è¦å¤šæ¬¡è¢«ç¼–å†™ï¼Œè€Œä¸æ˜¯åªç¼–å†™ä¸€æ¬¡å¾ªç¯ä¸»ä½“å†ä½¿ç”¨å¦ä¸€ä¸ª å¾ªç¯æ¥åå¤æ‰§è¡Œçš„ã€‚ä»»ä½•çš„å°é—­å¾ªç¯å¯å°†å®ƒçš„è¿­ä»£æ¬¡æ•°å‡å°‘æˆ–å®Œå…¨åˆ é™¤ã€‚å¾ªç¯ä½“çš„å¤åˆ¶æ•° é‡è¢«ç§°ä¸ºå¾ªç¯å±•å¼€å› å­ï¼Œè¿­ä»£æ¬¡æ•°å°±å˜ä¸ºäº†åŸå§‹å¾ªç¯è¿­ä»£æ¬¡æ•°é™¤ä»¥å¾ªç¯å±•å¼€å› å­ã€‚åœ¨é¡ºåº æ•°ç»„ä¸­ï¼Œå½“å¾ªç¯çš„è¿­ä»£æ¬¡æ•°åœ¨å¾ªç¯æ‰§è¡Œä¹‹å‰å°±å·²ç»çŸ¥é“æ—¶ï¼Œå¾ªç¯å±•å¼€æ˜¯æœ€æœ‰æ•ˆæå‡æ€§èƒ½çš„ æ–¹æ³•ã€‚è€ƒè™‘ä¸‹é¢çš„ä»£ç ï¼š

```c
 for (int i = 0; i < 100; i++) {
   a[i] = b[i] + c[i];
 }
```

å¦‚æœé‡å¤æ“ä½œä¸€æ¬¡å¾ªç¯ä½“ï¼Œè¿­ä»£æ¬¡æ•°èƒ½å‡å°‘åˆ°åŸå§‹å¾ªç¯çš„ä¸€åŠï¼š

```c
 for (int i = 0; i < 100; i += 2) {
   a[i]   = b[i]   + c[i];
   a[i+1] = b[i+1] + c[i+1];
 }
```

<mark style="color:red;">ä»é«˜çº§è¯­è¨€å±‚é¢ä¸Šæ¥çœ‹ï¼Œå¾ªç¯å±•å¼€ä½¿æ€§èƒ½æé«˜çš„åŸå› å¯èƒ½ä¸æ˜¯æ˜¾è€Œæ˜“è§çš„ã€‚è¿™ç§æå‡ æ¥è‡ªäºç¼–è¯‘å™¨æ‰§è¡Œå¾ªç¯å±•å¼€æ—¶ä½çº§æŒ‡ä»¤çš„æ”¹è¿›å’Œä¼˜åŒ–ã€‚ä¾‹å¦‚ï¼Œåœ¨å‰é¢å¾ªç¯å±•å¼€çš„ä¾‹å­ä¸­ï¼Œ æ¡ä»¶i<100åªæ£€æŸ¥äº†50æ¬¡ï¼Œè€Œåœ¨åŸæ¥çš„å¾ªç¯ä¸­åˆ™æ£€æŸ¥äº†100æ¬¡ã€‚å¦å¤–ï¼Œå› ä¸ºåœ¨æ¯ä¸ªå¾ªç¯ä¸­æ¯ ä¸ªè¯­å¥çš„è¯»å’Œå†™éƒ½æ˜¯ç‹¬ç«‹çš„ï¼Œæ‰€ä»¥CPUå¯ä»¥åŒæ—¶å‘å‡ºå†…å­˜æ“ä½œã€‚</mark>&#x20;

åœ¨CUDAä¸­ï¼Œå¾ªç¯å±•å¼€çš„æ„ä¹‰éå¸¸é‡å¤§ã€‚æˆ‘ä»¬çš„ç›®æ ‡ä»ç„¶æ˜¯ç›¸åŒçš„ï¼šé€šè¿‡å‡å°‘æŒ‡ä»¤æ¶ˆ è€—å’Œå¢åŠ æ›´å¤šçš„ç‹¬ç«‹è°ƒåº¦æŒ‡ä»¤æ¥æé«˜æ€§èƒ½ã€‚å› æ­¤ï¼Œæ›´å¤šçš„å¹¶å‘æ“ä½œè¢«æ·»åŠ åˆ°æµæ°´çº¿ä¸Šï¼Œä»¥ äº§ç”Ÿæ›´é«˜çš„æŒ‡ä»¤å’Œå†…å­˜å¸¦å®½ã€‚è¿™ä¸ºçº¿ç¨‹æŸè°ƒåº¦å™¨æä¾›æ›´å¤šç¬¦åˆæ¡ä»¶çš„çº¿ç¨‹æŸï¼Œå®ƒä»¬å¯ä»¥å¸® åŠ©éšè—æŒ‡ä»¤æˆ–å†…å­˜å»¶è¿Ÿã€‚

### å±•å¼€çš„å½’çº¦&#x20;

ä½ å¯èƒ½ä¼šæ³¨æ„åˆ°ï¼Œåœ¨reduceInterleavedæ ¸å‡½æ•°ä¸­æ¯ä¸ªçº¿ç¨‹å—åªå¤„ç†ä¸€éƒ¨åˆ†æ•°æ®ï¼Œè¿™äº› æ•°æ®å¯ä»¥è¢«è®¤ä¸ºæ˜¯ä¸€ä¸ªæ•°æ®å—ã€‚å¦‚æœç”¨ä¸€ä¸ªçº¿ç¨‹å—æ‰‹åŠ¨å±•å¼€ä¸¤ä¸ªæ•°æ®å—çš„å¤„ç†ï¼Œä¼šæ€ä¹ˆ æ ·ï¼Ÿä»¥ä¸‹çš„æ ¸å‡½æ•°æ˜¯reduceInterleavedæ ¸å‡½æ•°çš„ä¿®æ­£ç‰ˆï¼š<mark style="color:red;">æ¯ä¸ªçº¿ç¨‹å—æ±‡æ€»äº†æ¥è‡ªä¸¤ä¸ªæ•°æ® å—çš„æ•°æ®ã€‚è¿™æ˜¯ä¸€ä¸ªå¾ªç¯åˆ†åŒºçš„ä¾‹å­ï¼Œæ¯ä¸ªçº¿ç¨‹ä½œç”¨äºå¤šä¸ªæ•°æ® å—ï¼Œå¹¶å¤„ç†æ¯ä¸ªæ•°æ®å—çš„ä¸€ä¸ªå…ƒç´ ï¼š</mark>

```c
 __global__ void reduceUnrolling2 (int *g_idata, int *g_odata, unsigned int n) {
   // set thread ID
   unsigned int tid = threadIdx.x;
   unsigned int idx = blockIdx.x * blockDim.x * 2 + threadIdx.x;
   // convert global data pointer to the local pointer of this block
   // å¶æ•°å·blockçš„é¦–åœ°å€
   int *idata = g_idata + blockIdx.x * blockDim.x * 2;
   // unrolling 2 data blocks
   if (idx + blockDim.x < n) g_idata[idx] += g_idata[idx + blockDim.x];
   __syncthreads();
   // in-place reduction in global memory
   for (int stride = blockDim.x / 2; stride > 0; stride >>= 1) {
      if (tid < stride) {
         idata[tid] += idata[tid + stride];
      } 
     // synchronize within threadblock
      __syncthreads();
   }
   // write result for this block to global mem
   if (tid == 0) g_odata[blockIdx.x] = idata[0];
 }
```

æ³¨æ„è¦åœ¨æ ¸å‡½æ•°çš„å¼€å¤´æ·»åŠ çš„ä¸‹è¿°è¯­å¥ã€‚åœ¨è¿™é‡Œï¼Œæ¯ä¸ªçº¿ç¨‹éƒ½æ·»åŠ ä¸€ä¸ªæ¥è‡ªäºç›¸é‚»æ•° æ®å—çš„å…ƒç´ ã€‚ä»æ¦‚å¿µä¸Šæ¥è®²ï¼Œå¯ä»¥æŠŠå®ƒä½œä¸ºå½’çº¦å¾ªç¯çš„ä¸€ä¸ªè¿­ä»£ï¼Œæ­¤å¾ªç¯å¯åœ¨æ•°æ®å—é—´å½’ çº¦ï¼š

```c
if (idx + blockDim.x <n) g_idata[idx] += gidata[idx+blockDim.x];
```

å¦‚ä¸‹æ‰€ç¤ºï¼Œå…¨å±€æ•°ç»„ç´¢å¼•è¢«ç›¸åº”åœ°è°ƒæ•´ï¼Œå› ä¸ºåªéœ€è¦ä¸€åŠçš„çº¿ç¨‹å—æ¥å¤„ç†ç›¸åŒçš„æ•°æ® é›†ã€‚è¯·æ³¨æ„ï¼Œè¿™ä¹Ÿæ„å‘³ç€å¯¹äºç›¸åŒå¤§å°çš„æ•°æ®é›†ï¼Œå‘è®¾å¤‡æ˜¾ç¤ºçš„çº¿ç¨‹æŸå’Œçº¿ç¨‹å—çº§åˆ«çš„å¹¶ è¡Œæ€§æ›´ä½ã€‚

```c
 unsigned int idx = blockIdx.x * blockDim.x * 2 + threadIdx.x;
 int *idata = g_idata + blockIdx.x * blockDim.x * 2;
```

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (171).png" alt=""><figcaption></figcaption></figure>

{% hint style="info" %}
å› ä¸ºç°åœ¨æ¯ä¸ªçº¿ç¨‹å—å¤„ç†ä¸¤ä¸ªæ•°æ®å—ï¼Œæ‰€ä»¥éœ€è¦è°ƒæ•´å†…æ ¸çš„æ‰§è¡Œé…ç½®ï¼Œå°†ç½‘æ ¼å¤§å°å‡ å°è‡³ä¸€åŠ
{% endhint %}

### å±•å¼€çº¿ç¨‹è§„çº¦(Reducing with Unrolled Warps)

`__syncthreads`æ˜¯ç”¨äºå—å†…åŒæ­¥çš„ã€‚åœ¨å½’çº¦æ ¸å‡½æ•°ä¸­ï¼Œå®ƒç”¨æ¥ç¡®ä¿åœ¨çº¿ç¨‹è¿›å…¥ä¸‹ä¸€è½®ä¹‹ å‰ï¼Œæ¯ä¸€è½®ä¸­æ‰€æœ‰çº¿ç¨‹å·²ç»å°†å±€éƒ¨ç»“æœå†™å…¥å…¨å±€å†…å­˜ä¸­äº†ã€‚&#x20;

ç„¶è€Œï¼Œè¦ç»†æƒ³ä¸€ä¸‹åªå‰©ä¸‹32ä¸ªæˆ–æ›´å°‘çº¿ç¨‹ï¼ˆå³ä¸€ä¸ªçº¿ç¨‹æŸï¼‰çš„æƒ…å†µã€‚å› ä¸ºçº¿ç¨‹æŸçš„æ‰§ è¡Œæ˜¯SIMTï¼ˆå•æŒ‡ä»¤å¤šçº¿ç¨‹ï¼‰çš„ï¼Œæ¯æ¡æŒ‡ä»¤ä¹‹åæœ‰éšå¼çš„çº¿ç¨‹æŸå†…åŒæ­¥è¿‡ç¨‹ã€‚å› æ­¤ï¼Œå½’çº¦ å¾ªç¯çš„æœ€å6ä¸ªè¿­ä»£å¯ä»¥ç”¨ä¸‹è¿°è¯­å¥æ¥å±•å¼€ï¼š

```c
 if (tid < 32) {
   volatile int *vmem = idata;
   vmem[tid] += vmem[tid + 32];
   vmem[tid] += vmem[tid + 16];
   vmem[tid] += vmem[tid +  8];
   vmem[tid] += vmem[tid +  4];
   vmem[tid] += vmem[tid +  2];
   vmem[tid] += vmem[tid +  1];
 }
```

æ³¨æ„å˜é‡vmemæ˜¯å’Œvolatileä¿®é¥°ç¬¦ä¸€èµ·è¢«å£°æ˜çš„ï¼Œå®ƒå‘Šè¯‰ç¼–è¯‘å™¨æ¯æ¬¡èµ‹å€¼æ—¶å¿…é¡»å°† vmem\[tid]çš„å€¼å­˜å›å…¨å±€å†…å­˜ä¸­ã€‚å¦‚æœçœç•¥äº†volatileä¿®é¥°ç¬¦ï¼Œè¿™æ®µä»£ç å°†ä¸èƒ½æ­£å¸¸å·¥ä½œï¼Œ å› ä¸ºç¼–è¯‘å™¨æˆ–ç¼“å­˜å¯èƒ½å¯¹å…¨å±€æˆ–å…±äº«å†…å­˜ä¼˜åŒ–è¯»å†™ã€‚å¦‚æœä½äºå…¨å±€æˆ–å…±äº«å†…å­˜ä¸­çš„å˜é‡æœ‰ volatileä¿®é¥°ç¬¦ï¼Œç¼–è¯‘å™¨ä¼šå‡å®šå…¶å€¼å¯ä»¥è¢«å…¶ä»–çº¿ç¨‹åœ¨ä»»ä½•æ—¶é—´ä¿®æ”¹æˆ–ä½¿ç”¨ã€‚å› æ­¤ï¼Œä»»ä½•å¼•ç”¨volatileä¿®é¥°ç¬¦çš„å˜é‡å¼ºåˆ¶ç›´æ¥è¯»æˆ–å†™å†…å­˜ï¼Œè€Œä¸æ˜¯ç®€å•åœ°è¯»å†™ç¼“å­˜æˆ–å¯„å­˜å™¨ã€‚

{% hint style="info" %}
å¦‚æœçœç•¥äº†`volatile`ä¿®é¥°ç¬¦ï¼Œç¼–è¯‘å™¨å¯èƒ½ä¼šå¯¹å…±äº«å˜é‡çš„è¯»å†™æ“ä½œè¿›è¡Œä¼˜åŒ–ï¼Œä¾‹å¦‚å°†å…¶ç¼“å­˜åˆ°å¯„å­˜å™¨æˆ–è€…CPUç¼“å­˜ä¸­ï¼Œè€Œä¸æ˜¯æ¯æ¬¡éƒ½ç›´æ¥è¯»å†™å…¨å±€å†…å­˜ã€‚è¿™ç§ä¼˜åŒ–å¯èƒ½ä¼šå¯¼è‡´ä»¥ä¸‹é—®é¢˜ï¼š

1. **å¯è§æ€§é—®é¢˜ï¼š** åœ¨å¤šçº¿ç¨‹ç¯å¢ƒä¸‹ï¼Œå¦‚æœä¸€ä¸ªçº¿ç¨‹ä¿®æ”¹äº†å…±äº«å˜é‡çš„å€¼ï¼Œä½†æ˜¯è¿™ä¸ªä¿®æ”¹æ²¡æœ‰ç«‹å³å†™å›åˆ°å…¨å±€å†…å­˜ä¸­ï¼Œå…¶ä»–çº¿ç¨‹å°±æ— æ³•æ„ŸçŸ¥åˆ°è¿™ä¸ªä¿®æ”¹ï¼Œå¯¼è‡´äº†å¯è§æ€§é—®é¢˜ã€‚
2. **æ•°æ®ä¸ä¸€è‡´æ€§ï¼š** å¦‚æœå¤šä¸ªçº¿ç¨‹éƒ½ç¼“å­˜äº†å…±äº«å˜é‡çš„å€¼ï¼Œä½†æ˜¯å®ƒä»¬å„è‡ªçš„ç¼“å­˜å€¼å¯èƒ½ä¸å…¨å±€å†…å­˜ä¸­çš„å€¼ä¸ä¸€è‡´ï¼Œè¿™ä¼šå¯¼è‡´æ•°æ®ä¸ä¸€è‡´æ€§é—®é¢˜ã€‚
3. **ç«æ€æ¡ä»¶ï¼š** ç¼ºä¹åŒæ­¥æªæ–½çš„æƒ…å†µä¸‹ï¼Œå¤šä¸ªçº¿ç¨‹å¯¹å…±äº«å˜é‡è¿›è¡Œè¯»å†™æ“ä½œå¯èƒ½å¯¼è‡´ç«æ€æ¡ä»¶çš„å‘ç”Ÿï¼Œä»è€Œäº§ç”Ÿä¸ç¡®å®šçš„ç»“æœã€‚

é€šè¿‡ä½¿ç”¨`volatile`ä¿®é¥°ç¬¦ï¼Œå¯ä»¥å‘Šè¯‰ç¼–è¯‘å™¨ä¸è¦å¯¹è¿™äº›å˜é‡çš„è¯»å†™æ“ä½œè¿›è¡Œä¼˜åŒ–ï¼Œç¡®ä¿æ¯æ¬¡æ“ä½œéƒ½ç›´æ¥åæ˜ åˆ°å…¨å±€å†…å­˜ä¸­ï¼Œä»è€Œé¿å…äº†ä¸Šè¿°é—®é¢˜çš„å‘ç”Ÿã€‚
{% endhint %}

åŸºäºreduceUnrolling8ï¼Œçº¿ç¨‹æŸçš„å±•å¼€å¯ä»¥æ·»åŠ åˆ°å½’çº¦æ ¸å‡½æ•°ä¸­ï¼Œå¦‚ä¸‹æ‰€ç¤ºï¼š

```c
__global__ void reduceUnrollWarps8 (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 8 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 8;

    // unrolling 8
    if (idx + 7 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        int b1 = g_idata[idx + 4 * blockDim.x];
        int b2 = g_idata[idx + 5 * blockDim.x];
        int b3 = g_idata[idx + 6 * blockDim.x];
        int b4 = g_idata[idx + 7 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4 + b1 + b2 + b3 + b4;
    }

    __syncthreads();

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 32; stride >>= 1)// æ³¨æ„æ­¤å¤„çš„strideå¾ªç¯æ¡ä»¶å‘ç”Ÿäº†å˜åŒ–(ç›¸è¾ƒäºä¸Šé¢çš„å‡½æ•°)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }
    
    // unrolling warp
    if (tid < 32)
    {
        volatile int *vmem = idata;
        vmem[tid] += vmem[tid + 32];
        vmem[tid] += vmem[tid + 16];
        vmem[tid] += vmem[tid +  8];
        vmem[tid] += vmem[tid +  4];
        vmem[tid] += vmem[tid +  2];
        vmem[tid] += vmem[tid +  1];
    }
    /*
    ä¸Šé¢è¿™æ®µä»£ç å¯ä»¥çœ‹æˆ
    for (int stride = 32; stride > 0; stride >>= 1)// æ³¨æ„æ­¤å¤„çš„strideå¾ªç¯æ¡ä»¶å‘ç”Ÿäº†å˜åŒ–(ç›¸è¾ƒäºä¸Šé¢çš„å‡½æ•°)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }
    å®é™…ä¸Šå°±æ˜¯æŠŠä¸Šé¢çš„å¾ªç¯å±•å¼€äº†
    */
    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}
```

{% hint style="info" %}
å› ä¸ºåœ¨è¿™ä¸ªå®ç°ä¸­ï¼Œæ¯ä¸ªçº¿ç¨‹å¤„ç†8ä¸ªæ•°æ®å—ï¼Œè°ƒç”¨è¿™ä¸ªå†…æ ¸çš„åŒæ—¶å®ƒçš„ç½‘æ ¼å°ºå¯¸å‡ å°åˆ°1/8
{% endhint %}

### å®Œå…¨å±•å¼€çš„å½’çº¦&#x20;

å¦‚æœç¼–è¯‘æ—¶å·²çŸ¥ä¸€ä¸ªå¾ªç¯ä¸­çš„è¿­ä»£æ¬¡æ•°ï¼Œå°±å¯ä»¥æŠŠå¾ªç¯å®Œå…¨å±•å¼€ã€‚å› ä¸ºåœ¨Fermiæˆ– Kepleræ¶æ„ä¸­ï¼Œæ¯ä¸ªå—çš„æœ€å¤§çº¿ç¨‹æ•°éƒ½æ˜¯1024ï¼Œå¹¶ä¸”åœ¨è¿™äº›å½’çº¦æ ¸å‡½æ•°ä¸­ å¾ªç¯è¿­ä»£æ¬¡æ•°æ˜¯åŸºäºä¸€ä¸ªçº¿ç¨‹å—ç»´åº¦çš„ï¼Œæ‰€ä»¥å®Œå…¨å±•å¼€å½’çº¦å¾ªç¯æ˜¯å®Œå…¨å¯ä»¥çš„ï¼š

```c
__global__ void reduceCompleteUnrollWarps8 (int *g_idata, int *g_odata,
        unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 8 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 8;

    // unrolling 8
    if (idx + 7 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        int b1 = g_idata[idx + 4 * blockDim.x];
        int b2 = g_idata[idx + 5 * blockDim.x];
        int b3 = g_idata[idx + 6 * blockDim.x];
        int b4 = g_idata[idx + 7 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4 + b1 + b2 + b3 + b4;
    }

    __syncthreads();

    // in-place reduction and complete unroll
    if (blockDim.x >= 1024 && tid < 512) idata[tid] += idata[tid + 512];

    __syncthreads();

    if (blockDim.x >= 512 && tid < 256) idata[tid] += idata[tid + 256];

    __syncthreads();

    if (blockDim.x >= 256 && tid < 128) idata[tid] += idata[tid + 128];

    __syncthreads();

    if (blockDim.x >= 128 && tid < 64) idata[tid] += idata[tid + 64];

    __syncthreads();

    // unrolling warp
    if (tid < 32)
    {
        volatile int *vsmem = idata;
        vsmem[tid] += vsmem[tid + 32]; // tid = 0,1,2,3....
        vsmem[tid] += vsmem[tid + 16];
        vsmem[tid] += vsmem[tid +  8];
        vsmem[tid] += vsmem[tid +  4];
        vsmem[tid] += vsmem[tid +  2];
        vsmem[tid] += vsmem[tid +  1];
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}
```

### æ¨¡æ¿å‡½æ•°çš„å½’çº¦&#x20;

è™½ç„¶å¯ä»¥æ‰‹åŠ¨å±•å¼€å¾ªç¯ï¼Œä½†æ˜¯ä½¿ç”¨æ¨¡æ¿å‡½æ•°æœ‰åŠ©äºè¿›ä¸€æ­¥å‡å°‘åˆ†æ”¯æ¶ˆè€—ã€‚åœ¨è®¾å¤‡å‡½æ•° ä¸ŠCUDAæ”¯æŒæ¨¡æ¿å‚æ•°ã€‚å¦‚ä¸‹æ‰€ç¤ºï¼Œå¯ä»¥æŒ‡å®šå—çš„å¤§å°ä½œä¸ºæ¨¡æ¿å‡½æ•°çš„å‚æ•°ï¼š

```c
template <unsigned int iBlockSize>
__global__ void reduceCompleteUnroll(int *g_idata, int *g_odata,
                                     unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 8 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 8;

    // unrolling 8
    if (idx + 7 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        int b1 = g_idata[idx + 4 * blockDim.x];
        int b2 = g_idata[idx + 5 * blockDim.x];
        int b3 = g_idata[idx + 6 * blockDim.x];
        int b4 = g_idata[idx + 7 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4 + b1 + b2 + b3 + b4;
    }

    __syncthreads();

    // in-place reduction and complete unroll
    if (iBlockSize >= 1024 && tid < 512) idata[tid] += idata[tid + 512];

    __syncthreads();

    if (iBlockSize >= 512 && tid < 256)  idata[tid] += idata[tid + 256];

    __syncthreads();

    if (iBlockSize >= 256 && tid < 128)  idata[tid] += idata[tid + 128];

    __syncthreads();

    if (iBlockSize >= 128 && tid < 64)   idata[tid] += idata[tid + 64];

    __syncthreads();

    // unrolling warp
    if (tid < 32)
    {
        volatile int *vsmem = idata;
        vsmem[tid] += vsmem[tid + 32];
        vsmem[tid] += vsmem[tid + 16];
        vsmem[tid] += vsmem[tid +  8];
        vsmem[tid] += vsmem[tid +  4];
        vsmem[tid] += vsmem[tid +  2];
        vsmem[tid] += vsmem[tid +  1];
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}
```

ç›¸æ¯”reduceCompleteUnrollWarps8ï¼Œå”¯ä¸€çš„åŒºåˆ«æ˜¯ä½¿ç”¨äº†æ¨¡æ¿å‚æ•°æ›¿æ¢äº†å—å¤§å°ã€‚æ£€æŸ¥ å—å¤§å°çš„ifè¯­å¥å°†åœ¨ç¼–è¯‘æ—¶è¢«è¯„ä¼°ï¼Œå¦‚æœè¿™ä¸€æ¡ä»¶ä¸ºfalseï¼Œé‚£ä¹ˆç¼–è¯‘æ—¶å®ƒå°†ä¼šè¢«åˆ é™¤ï¼Œä½¿ å¾—å†…å¾ªç¯æ›´æœ‰æ•ˆç‡ã€‚ä¾‹å¦‚ï¼Œåœ¨çº¿ç¨‹å—å¤§å°ä¸º256çš„æƒ…å†µä¸‹è°ƒç”¨è¿™ä¸ªæ ¸å‡½æ•°ï¼Œä¸‹è¿°è¯­å¥å°†æ°¸ è¿œæ˜¯falseï¼š

```c
iBlockSize>=1024 && tid < 512
```

ç¼–è¯‘å™¨ä¼šè‡ªåŠ¨ä»æ‰§è¡Œå†…æ ¸ä¸­ç§»é™¤å®ƒã€‚ è¯¥æ ¸å‡½æ•°ä¸€å®šè¦åœ¨switch-caseç»“æ„ä¸­è¢«è°ƒç”¨ã€‚è¿™å…è®¸ç¼–è¯‘å™¨ä¸ºç‰¹å®šçš„çº¿ç¨‹å—å¤§å°è‡ªåŠ¨ ä¼˜åŒ–ä»£ç ï¼Œä½†è¿™ä¹Ÿæ„å‘³ç€å®ƒåªå¯¹åœ¨ç‰¹å®šå—å¤§å°ä¸‹å¯åŠ¨reduceCompleteUnrollæœ‰æ•ˆï¼š

```c
switch (blocksize) {
      case 1024:
         reduceCompleteUnroll<1024><<<grid.x/8, block>>>(d_idata, d_odata, size);
         break;
      case 512:
         reduceCompleteUnroll<512><<<grid.x/8, block>>>(d_idata, d_odata, size);
         break;
      case 256:
         reduceCompleteUnroll<256><<<grid.x/8, block>>>(d_idata, d_odata, size);
         break;
      case 128:
         reduceCompleteUnroll<128><<<grid.x/8, block>>>(d_idata, d_odata, size);
         break;
      case 64:
         reduceCompleteUnroll<64><<<grid.x/8, block>>>(d_idata, d_odata, size); 
         break;
   } 
```

ä¸‹è¡¨æ¦‚æ‹¬äº†æœ¬èŠ‚æåˆ°çš„æ‰€æœ‰å¹¶è¡Œå½’çº¦å®ç°çš„æ•ˆæœ

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (172).png" alt=""><figcaption></figcaption></figure>

ä¸‹è¡¨æ€»ç»“äº†æ‰€æœ‰æ ¸å‡½æ•°åŠ è½½/å­˜å‚¨æ•°æ®çš„æ•ˆç‡

<figure><img src="../../.gitbook/assets/å›¾ç‰‡ (173).png" alt=""><figcaption></figcaption></figure>

å®Œæ•´ä»£ç å¦‚ä¸‹ï¼š

```c
#include "../common/common.h"
#include <cuda_runtime.h>
#include <stdio.h>

/*
 * This code implements the interleaved and neighbor-paired approaches to
 * parallel reduction in CUDA. For this example, the sum operation is used. A
 * variety of optimizations on parallel reduction aimed at reducing divergence
 * are also demonstrated, such as unrolling.
 */

// Recursive Implementation of Interleaved Pair Approach
int recursiveReduce(int *data, int const size)
{
    // terminate check
    if (size == 1) return data[0];

    // renew the stride
    int const stride = size / 2;

    // in-place reduction
    for (int i = 0; i < stride; i++)
    {
        data[i] += data[i + stride];
    }

    // call recursively
    return recursiveReduce(data, stride);
}

// Neighbored Pair Implementation with divergence
__global__ void reduceNeighbored (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x;

    // boundary check
    if (idx >= n) return;

    // in-place reduction in global memory
    for (int stride = 1; stride < blockDim.x; stride *= 2)// æ§åˆ¶strideä¸å˜ï¼Œè®©tidå˜åŒ–
    {
        if ((tid % (2 * stride)) == 0) // tidä¸ºå¥‡æ•°çš„æƒ…å†µä¼šè¢«è¿‡æ»¤æ‰ï¼Œä¼šå‘ç”Ÿçº¿ç¨‹æŸåˆ†åŒ–
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

// Neighbored Pair Implementation with less divergence
__global__ void reduceNeighboredLess (int *g_idata, int *g_odata,
                                      unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x;

    // boundary check
    if(idx >= n) return;

    // in-place reduction in global memory
    for (int stride = 1; stride < blockDim.x; stride *= 2)
    {
        // convert tid into local array index
        int index = 2 * stride * tid;

        if (index < blockDim.x)
        {
            idata[index] += idata[index + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

// Interleaved Pair Implementation with less divergence
__global__ void reduceInterleaved (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x;

    // boundary check
    if(idx >= n) return;

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 0; stride >>= 1)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

__global__ void reduceUnrolling2 (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 2 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 2;

    // unrolling 2
    if (idx + blockDim.x < n) g_idata[idx] += g_idata[idx + blockDim.x];

    __syncthreads();

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 0; stride >>= 1)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];// idataæ˜¯å¶æ•°
        }

        // synchronize within threadblock
        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

__global__ void reduceUnrolling4 (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 4 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 4;

    // unrolling 4
    if (idx + 3 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4;
    }

    __syncthreads();

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 0; stride >>= 1)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

__global__ void reduceUnrolling8 (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 8 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 8;

    // unrolling 8
    if (idx + 7 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        int b1 = g_idata[idx + 4 * blockDim.x];
        int b2 = g_idata[idx + 5 * blockDim.x];
        int b3 = g_idata[idx + 6 * blockDim.x];
        int b4 = g_idata[idx + 7 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4 + b1 + b2 + b3 + b4;
    }

    __syncthreads();

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 0; stride >>= 1)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

__global__ void reduceUnrollWarps8 (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 8 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 8;

    // unrolling 8
    if (idx + 7 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        int b1 = g_idata[idx + 4 * blockDim.x];
        int b2 = g_idata[idx + 5 * blockDim.x];
        int b3 = g_idata[idx + 6 * blockDim.x];
        int b4 = g_idata[idx + 7 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4 + b1 + b2 + b3 + b4;
    }

    __syncthreads();

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 32; stride >>= 1)// æ³¨æ„æ­¤å¤„çš„strideå¾ªç¯æ¡ä»¶å‘ç”Ÿäº†å˜åŒ–(ç›¸è¾ƒäºä¸Šé¢çš„å‡½æ•°)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }
    
    // unrolling warp
    if (tid < 32)
    {
        volatile int *vmem = idata;
        vmem[tid] += vmem[tid + 32];
        vmem[tid] += vmem[tid + 16];
        vmem[tid] += vmem[tid +  8];
        vmem[tid] += vmem[tid +  4];
        vmem[tid] += vmem[tid +  2];
        vmem[tid] += vmem[tid +  1];
    }
    /*
    ä¸Šé¢è¿™æ®µä»£ç å¯ä»¥çœ‹æˆ
    for (int stride = 32; stride > 0; stride >>= 1)// æ³¨æ„æ­¤å¤„çš„strideå¾ªç¯æ¡ä»¶å‘ç”Ÿäº†å˜åŒ–(ç›¸è¾ƒäºä¸Šé¢çš„å‡½æ•°)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }
    å®é™…ä¸Šå°±æ˜¯æŠŠä¸Šé¢çš„å¾ªç¯å±•å¼€äº†
    */
    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

__global__ void reduceCompleteUnrollWarps8 (int *g_idata, int *g_odata,
        unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 8 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 8;

    // unrolling 8
    if (idx + 7 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        int b1 = g_idata[idx + 4 * blockDim.x];
        int b2 = g_idata[idx + 5 * blockDim.x];
        int b3 = g_idata[idx + 6 * blockDim.x];
        int b4 = g_idata[idx + 7 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4 + b1 + b2 + b3 + b4;
    }

    __syncthreads();

    // in-place reduction and complete unroll
    if (blockDim.x >= 1024 && tid < 512) idata[tid] += idata[tid + 512];

    __syncthreads();

    if (blockDim.x >= 512 && tid < 256) idata[tid] += idata[tid + 256];

    __syncthreads();

    if (blockDim.x >= 256 && tid < 128) idata[tid] += idata[tid + 128];

    __syncthreads();

    if (blockDim.x >= 128 && tid < 64) idata[tid] += idata[tid + 64];

    __syncthreads();

    // unrolling warp
    if (tid < 32)
    {
        volatile int *vsmem = idata;
        vsmem[tid] += vsmem[tid + 32]; // tid = 0,1,2,3....
        vsmem[tid] += vsmem[tid + 16];
        vsmem[tid] += vsmem[tid +  8];
        vsmem[tid] += vsmem[tid +  4];
        vsmem[tid] += vsmem[tid +  2];
        vsmem[tid] += vsmem[tid +  1];
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

template <unsigned int iBlockSize>
__global__ void reduceCompleteUnroll(int *g_idata, int *g_odata,
                                     unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 8 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 8;

    // unrolling 8
    if (idx + 7 * blockDim.x < n)
    {
        int a1 = g_idata[idx];
        int a2 = g_idata[idx + blockDim.x];
        int a3 = g_idata[idx + 2 * blockDim.x];
        int a4 = g_idata[idx + 3 * blockDim.x];
        int b1 = g_idata[idx + 4 * blockDim.x];
        int b2 = g_idata[idx + 5 * blockDim.x];
        int b3 = g_idata[idx + 6 * blockDim.x];
        int b4 = g_idata[idx + 7 * blockDim.x];
        g_idata[idx] = a1 + a2 + a3 + a4 + b1 + b2 + b3 + b4;
    }

    __syncthreads();

    // in-place reduction and complete unroll
    if (iBlockSize >= 1024 && tid < 512) idata[tid] += idata[tid + 512];

    __syncthreads();

    if (iBlockSize >= 512 && tid < 256)  idata[tid] += idata[tid + 256];

    __syncthreads();

    if (iBlockSize >= 256 && tid < 128)  idata[tid] += idata[tid + 128];

    __syncthreads();

    if (iBlockSize >= 128 && tid < 64)   idata[tid] += idata[tid + 64];

    __syncthreads();

    // unrolling warp
    if (tid < 32)
    {
        volatile int *vsmem = idata;
        vsmem[tid] += vsmem[tid + 32];
        vsmem[tid] += vsmem[tid + 16];
        vsmem[tid] += vsmem[tid +  8];
        vsmem[tid] += vsmem[tid +  4];
        vsmem[tid] += vsmem[tid +  2];
        vsmem[tid] += vsmem[tid +  1];
    }

    // write result for this block to global mem
    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

__global__ void reduceUnrollWarps (int *g_idata, int *g_odata, unsigned int n)
{
    // set thread ID
    unsigned int tid = threadIdx.x;
    unsigned int idx = blockIdx.x * blockDim.x * 2 + threadIdx.x;

    // convert global data pointer to the local pointer of this block
    int *idata = g_idata + blockIdx.x * blockDim.x * 2;

    // unrolling 2
    if (idx + blockDim.x < n) g_idata[idx] += g_idata[idx + blockDim.x];

    __syncthreads();

    // in-place reduction in global memory
    for (int stride = blockDim.x / 2; stride > 32; stride >>= 1)
    {
        if (tid < stride)
        {
            idata[tid] += idata[tid + stride];
        }

        // synchronize within threadblock
        __syncthreads();
    }

    // unrolling last warp
    if (tid < 32)
    {
        volatile int *vsmem = idata;
        vsmem[tid] += vsmem[tid + 32];
        vsmem[tid] += vsmem[tid + 16];
        vsmem[tid] += vsmem[tid +  8];
        vsmem[tid] += vsmem[tid +  4];
        vsmem[tid] += vsmem[tid +  2];
        vsmem[tid] += vsmem[tid +  1];
    }

    if (tid == 0) g_odata[blockIdx.x] = idata[0];
}

int main(int argc, char **argv)
{
    // set up device
    int dev = 0;
    cudaDeviceProp deviceProp;
    CHECK(cudaGetDeviceProperties(&deviceProp, dev));
    printf("%s starting reduction at ", argv[0]);
    printf("device %d: %s ", dev, deviceProp.name);
    CHECK(cudaSetDevice(dev));

    bool bResult = false;

    // initialization
    int size = 1 << 24; // total number of elements to reduce
    printf("    with array size %d  ", size);

    // execution configuration
    int blocksize = 512;   // initial block size

    if(argc > 1)
    {
        blocksize = atoi(argv[1]);   // block size from command line argument
    }

    dim3 block (blocksize, 1);
    dim3 grid  ((size + block.x - 1) / block.x, 1);
    printf("grid %d block %d\n", grid.x, block.x);

    // allocate host memory
    size_t bytes = size * sizeof(int);
    int *h_idata = (int *) malloc(bytes);
    int *h_odata = (int *) malloc(grid.x * sizeof(int));
    int *tmp     = (int *) malloc(bytes);

    // initialize the array
    for (int i = 0; i < size; i++)
    {
        // mask off high 2 bytes to force max number to 255
        h_idata[i] = (int)( rand() & 0xFF );
    }

    memcpy (tmp, h_idata, bytes);

    double iStart, iElaps;
    int gpu_sum = 0;

    // allocate device memory
    int *d_idata = NULL;
    int *d_odata = NULL;
    CHECK(cudaMalloc((void **) &d_idata, bytes));
    CHECK(cudaMalloc((void **) &d_odata, grid.x * sizeof(int)));

    // cpu reduction
    iStart = seconds();
    int cpu_sum = recursiveReduce (tmp, size);
    iElaps = seconds() - iStart;
    printf("cpu reduce      elapsed %f sec cpu_sum: %d\n", iElaps, cpu_sum);

    // kernel 1: reduceNeighbored
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceNeighbored<<<grid, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x; i++) gpu_sum += h_odata[i];

    printf("gpu Neighbored  elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x, block.x);

    // kernel 2: reduceNeighbored with less divergence
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceNeighboredLess<<<grid, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x; i++) gpu_sum += h_odata[i];

    printf("gpu Neighbored2 elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x, block.x);

    // kernel 3: reduceInterleaved
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceInterleaved<<<grid, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x; i++) gpu_sum += h_odata[i];

    printf("gpu Interleaved elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x, block.x);

    // kernel 4: reduceUnrolling2
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceUnrolling2<<<grid.x / 2, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x / 2 * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x / 2; i++) gpu_sum += h_odata[i];

    printf("gpu Unrolling2  elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x / 2, block.x);

    // kernel 5: reduceUnrolling4
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceUnrolling4<<<grid.x / 4, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x / 4 * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x / 4; i++) gpu_sum += h_odata[i];

    printf("gpu Unrolling4  elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x / 4, block.x);

    // kernel 6: reduceUnrolling8
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceUnrolling8<<<grid.x / 8, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x / 8 * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x / 8; i++) gpu_sum += h_odata[i];

    printf("gpu Unrolling8  elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x / 8, block.x);

    for (int i = 0; i < grid.x / 16; i++) gpu_sum += h_odata[i];

    // kernel 8: reduceUnrollWarps8
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceUnrollWarps8<<<grid.x / 8, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x / 8 * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x / 8; i++) gpu_sum += h_odata[i];

    printf("gpu UnrollWarp8 elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x / 8, block.x);


    // kernel 9: reduceCompleteUnrollWarsp8
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();
    reduceCompleteUnrollWarps8<<<grid.x / 8, block>>>(d_idata, d_odata, size);
    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x / 8 * sizeof(int),
                     cudaMemcpyDeviceToHost));
    gpu_sum = 0;

    for (int i = 0; i < grid.x / 8; i++) gpu_sum += h_odata[i];

    printf("gpu Cmptnroll8  elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x / 8, block.x);

    // kernel 9: reduceCompleteUnroll
    CHECK(cudaMemcpy(d_idata, h_idata, bytes, cudaMemcpyHostToDevice));
    CHECK(cudaDeviceSynchronize());
    iStart = seconds();

    switch (blocksize)
    {
    case 1024:
        reduceCompleteUnroll<1024><<<grid.x / 8, block>>>(d_idata, d_odata,
                size);
        break;

    case 512:
        reduceCompleteUnroll<512><<<grid.x / 8, block>>>(d_idata, d_odata,
                size);
        break;

    case 256:
        reduceCompleteUnroll<256><<<grid.x / 8, block>>>(d_idata, d_odata,
                size);
        break;

    case 128:
        reduceCompleteUnroll<128><<<grid.x / 8, block>>>(d_idata, d_odata,
                size);
        break;

    case 64:
        reduceCompleteUnroll<64><<<grid.x / 8, block>>>(d_idata, d_odata, size);
        break;
    }

    CHECK(cudaDeviceSynchronize());
    iElaps = seconds() - iStart;
    CHECK(cudaMemcpy(h_odata, d_odata, grid.x / 8 * sizeof(int),
                     cudaMemcpyDeviceToHost));

    gpu_sum = 0;

    for (int i = 0; i < grid.x / 8; i++) gpu_sum += h_odata[i];

    printf("gpu Cmptnroll   elapsed %f sec gpu_sum: %d <<<grid %d block "
           "%d>>>\n", iElaps, gpu_sum, grid.x / 8, block.x);

    // free host memory
    free(h_idata);
    free(h_odata);

    // free device memory
    CHECK(cudaFree(d_idata));
    CHECK(cudaFree(d_odata));

    // reset device
    CHECK(cudaDeviceReset());

    // check the results
    bResult = (gpu_sum == cpu_sum);

    if(!bResult) printf("Test failed!\n");

    return EXIT_SUCCESS;
}
```
